apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion project.ANDROID_COMPILE_SDK_VERSION as int
    buildToolsVersion project.ANDROID_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.cmy.demo.adtest.my"
        minSdkVersion project.ANDROID_BUILD_MIN_SDK_VERSION as int
        targetSdkVersion project.ANDROID_BUILD_TARGET_SDK_VERSION as int
        versionCode project.VERSION_CODE as int
        versionName project.VERSION_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a','x86'
        }

        //ARouter路由框架配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        //开发版(应用开发阶段选择此版本)
        dev {
            //如果依赖的模块中没有相对应的构建版本,则构建debug版本
            matchingFallbacks = ['debug']
        }
        //内测版(提交给公司测试选择此版本)
        alpha{
            matchingFallbacks = ['debug']
        }
        //测试版(内测完成后的灰度测试版本)
        beta{
            //如果依赖的模块中没有相对应的构建版本,则构建release版本
            matchingFallbacks = ['release']
        }
        //正式版(分发给所有用户的版本)
        release {
            //正式版开启混淆
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    annotationProcessor "com.alibaba:arouter-compiler:${arouter_compiler_version}"
    api "com.leon.channel:helper:$channel_version"

    implementation project(path: ':common')
    implementation project(path: ':domain')
}